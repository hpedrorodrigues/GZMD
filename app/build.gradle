apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def getStoreFilePath() {
    def storeFilePath = System.getenv("GZMD_STORE_FILE")
    return (storeFilePath == null || storeFilePath.isEmpty()) ? "gzmd_debug.keystore" : storeFilePath
}

android {

    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {

        applicationId "com.hpedrorodrigues.gizmodobr"

        minSdkVersion 15
        targetSdkVersion 24

        versionCode 2
        versionName "1.0.1"
    }

    signingConfigs {

        release {

            storeFile file(getStoreFilePath())
            storePassword System.getenv("GZMD_STORE_PASSWORD")
            keyAlias System.getenv("GZMD_STORE_KEY_ALIAS")
            keyPassword System.getenv("GZMD_KEY_PASSWORD")
        }
    }

    buildTypes {

        release {

            signingConfig signingConfigs.release

            shrinkResources true
            useProguard true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles 'proguard-rules.pro'
        }

        debug {

            shrinkResources true
            useProguard true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles 'proguard-rules.pro'
        }
    }

    sourceSets {

        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    lintOptions {

        abortOnError false
    }
}

kapt {

    generateStubs = true
}

buildscript {

    repositories {

        jcenter()
        mavenCentral()
    }

    dependencies {

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

repositories {

    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {

    // - Kotlin dependencies
    compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: kotlin_version

    // - Android basic dependencies
    compile group: "com.android.support", name: "appcompat-v7", version: support_library_version
    compile group: "com.android.support", name: "design", version: support_library_version
    compile group: "com.android.support", name: "cardview-v7", version: support_library_version
    compile group: "com.android.support", name: "recyclerview-v7", version: support_library_version
    compile group: "com.android.support", name: "palette-v7", version: support_library_version

    // - Super Recycler View
    compile group: 'com.malinskiy', name: 'superrecyclerview', version: '1.1.4'

    // - Label View
    compile group: 'com.github.linger1216', name: 'labelview', version: 'v1.1.0'

    // - Image Loading
    compile group: 'com.squareup.picasso', name: 'picasso', version: '2.5.2'

    // - Licenses Dialog
    compile(group: 'de.psdev.licensesdialog', name: 'licensesdialog', version: '1.8.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    // - Discrete Seekbar
    compile group: 'org.adw.library', name: 'discrete-seekbar', version: '1.0.1'

    // - Loading Library
    compile group: 'com.wang.avi', name: 'library', version: '1.0.5'
    compile group: 'com.nineoldandroids', name: 'library', version: '2.4.0'

    // - Jsoup
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.3'

    // - Dagger 2 dependencies
    compile group: 'com.google.dagger', name: 'dagger', version: '2.2'
    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: '2.2'
    provided group: 'org.glassfish', name: 'javax.annotation', version: '10.0-b28'

    // - Networking dependencies
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.1.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.1.0'
    compile group: 'com.squareup.retrofit2', name: 'adapter-rxjava', version: '2.1.0'

    // - Rx dependencies
    compile group: 'io.reactivex', name: 'rxandroid', version: '1.2.1'
    compile group: 'io.reactivex', name: 'rxjava', version: '1.1.6'
    compile group: 'io.reactivex', name: 'rxkotlin', version: '0.60.0'

    // - Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true
    }

    // - Analytics
    compile group: 'com.google.android.gms', name: 'play-services-analytics', version: '9.0.1'

    // - Firebase
    compile group: 'com.google.firebase', name: 'firebase-messaging', version: '9.0.1'

    // - Test dependencies
    androidTestCompile group: 'commons-io', name: 'commons-io', version: '2.4'
    androidTestCompile group: 'com.android.support.test', name: 'testing-support-lib', version: '0.1'
    androidTestCompile group: 'com.android.support.test.espresso', name: 'espresso-core', version: '2.0'
}

apply plugin: 'com.google.gms.google-services'